(in-package #:net.mfiano.lisp.origin.vec2)

(u:fn-> = (vec vec &key (:rel u:f32) (:abs u:f32)) boolean)
(declaim (inline =))
(defun = (vec1 vec2 &key (rel 1e-7) (abs rel))
  (declare (optimize speed))
  (com:cwcmp 2 (vec1 vec2) (com:= vec1 vec2 rel abs)))

(u:fn-> zero! (vec) vec)
(declaim (inline zero!))
(defun zero! (vec)
  (declare (optimize speed))
  (com:cwset 2 vec nil 0f0)
  vec)

(u:fn-> zero () vec)
(declaim (inline zero))
(defun zero ()
  (declare (optimize speed))
  (%vec 0f0 0f0))

(u:fn-> zero-p (vec) boolean)
(declaim (inline zero-p))
(defun zero-p (vec)
  (declare (optimize speed))
  (= vec +zero+))

(u:fn-> random! (vec u:f32 u:f32) vec)
(declaim (inline random!))
(defun random! (out min max)
  (declare (optimize speed))
  (let ((diff (cl:- max min)))
    (com:cwset 2 out nil (cl:+ min (cl:random diff))))
  out)

(u:fn-> random (u:f32 u:f32) vec)
(declaim (inline random))
(defun random (min max)
  (declare (optimize speed))
  (random! (zero) min max))

(u:fn-> copy! (vec vec) vec)
(declaim (inline copy!))
(defun copy! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec vec)
  out)

(u:fn-> copy (vec) vec)
(declaim (inline copy))
(defun copy (vec)
  (declare (optimize speed))
  (copy! (zero) vec))

(u:fn-> sign! (vec vec) vec)
(declaim (inline sign!))
(defun sign! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (signum vec))
  out)

(u:fn-> sign (vec) vec)
(declaim (inline sign))
(defun sign (vec)
  (declare (optimize speed))
  (sign! (zero) vec))

(u:fn-> fract! (vec vec) vec)
(declaim (inline fract!))
(defun fract! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:- vec (ffloor vec)))
  out)

(u:fn-> fract (vec) vec)
(declaim (inline fract))
(defun fract (vec)
  (declare (optimize speed))
  (fract! (zero) vec))

(u:fn-> clamp! (vec vec u:f32 u:f32) vec)
(declaim (inline clamp!))
(defun clamp! (out vec min max)
  (declare (optimize speed))
  (com:cwset 2 out vec (u:clamp vec min max))
  out)

(u:fn-> clamp (vec u:f32 u:f32) vec)
(declaim (inline clamp))
(defun clamp (vec min max)
  (declare (optimize speed))
  (clamp! (zero) vec min max))

(u:fn-> +! (vec vec vec) vec)
(declaim (inline +!))
(defun +! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (cl:+ vec1 vec2))
  out)

(u:fn-> + (vec vec) vec)
(declaim (inline +))
(defun + (vec1 vec2)
  (declare (optimize speed))
  (+! (zero) vec1 vec2))

(u:fn-> -! (vec vec vec) vec)
(declaim (inline -!))
(defun -! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (cl:- vec1 vec2))
  out)

(u:fn-> - (vec vec) vec)
(declaim (inline -))
(defun - (vec1 vec2)
  (declare (optimize speed))
  (-! (zero) vec1 vec2))

(u:fn-> *! (vec vec vec) vec)
(declaim (inline *!))
(defun *! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (cl:* vec1 vec2))
  out)

(u:fn-> * (vec vec) vec)
(declaim (inline *))
(defun * (vec1 vec2)
  (declare (optimize speed))
  (*! (zero) vec1 vec2))

(u:fn-> /! (vec vec vec) vec)
(declaim (inline /!))
(defun /! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (if (zerop vec2) 0f0 (cl:/ vec1 vec2)))
  out)

(u:fn-> / (vec vec) vec)
(declaim (inline /))
(defun / (vec1 vec2)
  (declare (optimize speed))
  (/! (zero) vec1 vec2))

(defmacro %scale (ox oy x y scalar)
  `(psetf ,ox (cl:* ,x ,scalar)
          ,oy (cl:* ,y ,scalar)))

(u:fn-> scale! (vec vec u:f32) vec)
(declaim (inline scale!))
(defun scale! (out vec scalar)
  (declare (optimize speed))
  (with-components ((o out) (v vec))
    (%scale ox oy vx vy scalar))
  out)

(u:fn-> scale (vec u:f32) vec)
(declaim (inline scale))
(defun scale (vec scalar)
  (declare (optimize speed))
  (scale! (zero) vec scalar))

(u:fn-> invert! (vec vec) vec)
(declaim (inline invert!))
(defun invert! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (if (zerop vec) 0f0 (cl:/ vec)))
  out)

(u:fn-> invert (vec) vec)
(declaim (inline invert))
(defun invert (vec)
  (declare (optimize speed))
  (invert! (zero) vec))

(defmacro %dot (v1x v1y v2x v2y)
  `(cl:+ (cl:* ,v1x ,v2x) (cl:* ,v1y ,v2y)))

(u:fn-> dot (vec vec) u:f32)
(declaim (inline dot))
(defun dot (vec1 vec2)
  (declare (optimize speed))
  (with-components ((v1 vec1) (v2 vec2))
    (%dot v1x v1y v2x v2y)))

(defmacro %length-squared (x y)
  ;; NOTE: This is not using %DOT because using * instead of EXPT and SBCL 1.5.9
  ;; cannot correctly infer the type of the SQRT of the sum of squares as being
  ;; a single-float. This is because SBCL's memory model policy is "everything
  ;; is volatile", which is acceptable because two AREF calls to the same array
  ;; may infact produce different values when threading is involved.
  `(cl:+ (cl:expt ,x 2) (cl:expt ,y 2)))

(u:fn-> length-squared (vec) u:f32)
(declaim (inline length-squared))
(defun length-squared (vec)
  (declare (optimize speed))
  (with-components ((v vec))
    (%length-squared vx vy)))

(defmacro %length (x y)
  `(cl:sqrt (%length-squared ,x ,y)))

(u:fn-> length (vec) u:f32)
(declaim (inline length))
(defun length (vec)
  (declare (optimize speed))
  (cl:sqrt (length-squared vec)))

(u:fn-> distance-squared (vec vec) u:f32)
(declaim (inline distance-squared))
(defun distance-squared (vec1 vec2)
  (declare (optimize speed))
  (length-squared (- vec2 vec1)))

(u:fn-> distance (vec vec) u:f32)
(declaim (inline distance))
(defun distance (vec1 vec2)
  (declare (optimize speed))
  (cl:sqrt (distance-squared vec1 vec2)))

(defmacro %normalize (ox oy x y)
  (u:with-gensyms (length inv-length)
    `(let ((,length (%length ,x ,y)))
       (unless (zerop ,length)
         (let ((,inv-length (cl:/ ,length)))
           (%scale ,ox ,oy ,x ,y ,inv-length))))))

(u:fn-> normalize! (vec vec) vec)
(declaim (inline normalize!))
(defun normalize! (out vec)
  (declare (optimize speed))
  (with-components ((o out) (v vec))
    (%normalize ox oy vx vy))
  out)

(u:fn-> normalize (vec) vec)
(declaim (inline normalize))
(defun normalize (vec)
  (declare (optimize speed))
  (normalize! (zero) vec))

(u:fn-> round! (vec vec) vec)
(declaim (inline round!))
(defun round! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (fround vec))
  out)

(u:fn-> round (vec) vec)
(declaim (inline round))
(defun round (vec)
  (declare (optimize speed))
  (round! (zero) vec))

(u:fn-> abs! (vec vec) vec)
(declaim (inline abs!))
(defun abs! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:abs vec))
  out)

(u:fn-> abs (vec) vec)
(declaim (inline abs))
(defun abs (vec)
  (declare (optimize speed))
  (abs! (zero) vec))

(u:fn-> negate! (vec vec) vec)
(declaim (inline negate!))
(defun negate! (out vec)
  (declare (optimize speed))
  (scale! out vec -1f0))

(u:fn-> negate (vec) vec)
(declaim (inline negate))
(defun negate (vec)
  (declare (optimize speed))
  (negate! (zero) vec))

(u:fn-> angle (vec vec) u:f32)
(declaim (inline angle))
(defun angle (vec1 vec2)
  (declare (optimize speed))
  (let ((dot (dot vec1 vec2))
        (m*m (cl:* (length vec1) (length vec2))))
    (if (zerop m*m)
        0f0
        (cl:acos (the (single-float -1f0 1f0) (cl:/ dot m*m))))))

(u:fn-> direction= (vec vec) boolean)
(declaim (inline direction=))
(defun direction= (vec1 vec2)
  (declare (optimize speed))
  (cl:>= (dot (normalize vec1) (normalize vec2)) (cl:- 1 1e-7)))

(u:fn-> lerp! (vec vec vec u:f32) vec)
(declaim (inline lerp!))
(defun lerp! (out vec1 vec2 factor)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (u:lerp factor vec1 vec2))
  out)

(u:fn-> lerp (vec vec u:f32) vec)
(declaim (inline lerp))
(defun lerp (vec1 vec2 factor)
  (declare (optimize speed))
  (lerp! (zero) vec1 vec2 factor))

(u:fn-> < (vec vec) boolean)
(declaim (inline <))
(defun < (vec1 vec2)
  (declare (optimize speed))
  (com:cwcmp 2 (vec1 vec2) (cl:< vec1 vec2)))

(u:fn-> <= (vec vec) boolean)
(declaim (inline <=))
(defun <= (vec1 vec2)
  (declare (optimize speed))
  (com:cwcmp 2 (vec1 vec2) (cl:<= vec1 vec2)))

(u:fn-> > (vec vec) boolean)
(declaim (inline >))
(defun > (vec1 vec2)
  (declare (optimize speed))
  (com:cwcmp 2 (vec1 vec2) (cl:> vec1 vec2)))

(u:fn-> >= (vec vec) boolean)
(declaim (inline >=))
(defun >= (vec1 vec2)
  (declare (optimize speed))
  (com:cwcmp 2 (vec1 vec2) (cl:>= vec1 vec2)))

(u:fn-> min! (vec vec vec) vec)
(declaim (inline min!))
(defun min! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (cl:min vec1 vec2))
  out)

(u:fn-> min (vec vec) vec)
(declaim (inline min))
(defun min (vec1 vec2)
  (declare (optimize speed))
  (min! (zero) vec1 vec2))

(u:fn-> max! (vec vec vec) vec)
(declaim (inline max!))
(defun max! (out vec1 vec2)
  (declare (optimize speed))
  (com:cwset 2 out (vec1 vec2) (cl:max vec1 vec2))
  out)

(u:fn-> max (vec vec) vec)
(declaim (inline max))
(defun max (vec1 vec2)
  (declare (optimize speed))
  (max! (zero) vec1 vec2))

(u:fn-> from-radians! (vec (single-float #.(cl:- com:pi) #.com:pi)) vec)
(declaim (inline from-radians!))
(defun from-radians! (vec radians)
  (declare (optimize speed))
  (with-components ((o vec))
    (psetf ox (cl:cos radians)
           oy (cl:sin radians))
    vec))

(u:fn-> from-radians ((single-float #.(cl:- com:pi) #.com:pi)) vec)
(declaim (inline from-radians))
(defun from-radians (radians)
  (declare (optimize speed))
  (from-radians! (zero) radians))

(u:fn-> radians! (vec vec) vec)
(declaim (inline radians!))
(defun radians! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:* vec com:+deg+))
  out)

(u:fn-> radians (vec) vec)
(declaim (inline radians))
(defun radians (vec)
  (declare (optimize speed))
  (radians! (zero) vec))

(u:fn-> from-degrees! (vec (single-float -360f0 360f0)) vec)
(declaim (inline from-degrees!))
(defun from-degrees! (out degrees)
  (declare (optimize speed))
  (with-components ((o out))
    (psetf ox (cl:cos (cl:* degrees com:+deg+))
           oy (cl:sin (cl:* degrees com:+deg+))))
  out)

(u:fn-> from-degrees ((single-float -360f0 360f0)) vec)
(declaim (inline from-degrees))
(defun from-degrees (degrees)
  (declare (optimize speed))
  (from-degrees! (zero) degrees))

(u:fn-> degrees! (vec vec) vec)
(declaim (inline degrees!))
(defun degrees! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:* vec com:+rad+))
  out)

(u:fn-> degrees (vec) vec)
(declaim (inline degrees))
(defun degrees (vec)
  (declare (optimize speed))
  (degrees! (zero) vec))

(u:fn-> expt! (vec vec real) vec)
(declaim (inline expt!))
(defun expt! (out vec power)
  (com:cwset 2 out vec (cl:expt vec power))
  out)

(u:fn-> expt (vec real) vec)
(declaim (inline expt))
(defun expt (vec power)
  (expt! (zero) vec power))

(u:fn-> sqrt! (vec vec) vec)
(declaim (inline sqrt!))
(defun sqrt! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:sqrt (the (single-float 0f0) vec)))
  out)

(u:fn-> sqrt (vec) vec)
(declaim (inline sqrt))
(defun sqrt (vec)
  (declare (optimize speed))
  (sqrt! (zero) vec))

(u:fn-> floor! (vec vec) vec)
(declaim (inline floor!))
(defun floor! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (ffloor vec))
  out)

(u:fn-> floor (vec) vec)
(declaim (inline floor))
(defun floor (vec)
  (declare (optimize speed))
  (floor! (zero) vec))

(u:fn-> ceiling! (vec vec) vec)
(declaim (inline ceiling!))
(defun ceiling! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (fceiling vec))
  out)

(u:fn-> ceiling (vec) vec)
(declaim (inline ceiling))
(defun ceiling (vec)
  (declare (optimize speed))
  (ceiling! (zero) vec))

(u:fn-> mod! (vec vec u:f32) vec)
(declaim (inline mod!))
(defun mod! (out vec divisor)
  (declare (optimize speed))
  (com:cwset 2 out vec (nth-value 1 (ffloor vec divisor)))
  out)

(u:fn-> mod (vec u:f32) vec)
(declaim (inline mod))
(defun mod (vec divisor)
  (declare (optimize speed))
  (mod! (zero) vec divisor))

(u:fn-> sin! (vec vec) vec)
(declaim (inline sin!))
(defun sin! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:sin vec))
  out)

(u:fn-> sin (vec) vec)
(declaim (inline sin))
(defun sin (vec)
  (declare (optimize speed))
  (sin! (zero) vec))

(u:fn-> cos! (vec vec) vec)
(declaim (inline cos!))
(defun cos! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:cos vec))
  out)

(u:fn-> cos (vec) vec)
(declaim (inline cos))
(defun cos (vec)
  (declare (optimize speed))
  (cos! (zero) vec))

(u:fn-> tan! (vec vec) vec)
(declaim (inline tan!))
(defun tan! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:tan vec))
  out)

(u:fn-> tan (vec) vec)
(declaim (inline tan))
(defun tan (vec)
  (declare (optimize speed))
  (tan! (zero) vec))

(u:fn-> asin! (vec vec) vec)
(declaim (inline asin!))
(defun asin! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:asin (the (single-float -1f0 1f0) vec)))
  out)

(u:fn-> asin (vec) vec)
(declaim (inline asin))
(defun asin (vec)
  (declare (optimize speed))
  (asin! (zero) vec))

(u:fn-> acos! (vec vec) vec)
(declaim (inline acos!))
(defun acos! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:acos (the (single-float -1f0 1f0) vec)))
  out)

(u:fn-> acos (vec) vec)
(declaim (inline acos))
(defun acos (vec)
  (declare (optimize speed))
  (acos! (zero) vec))

(u:fn-> atan! (vec vec) vec)
(declaim (inline atan!))
(defun atan! (out vec)
  (declare (optimize speed))
  (com:cwset 2 out vec (cl:atan vec))
  out)

(u:fn-> atan (vec) vec)
(declaim (inline atan))
(defun atan (vec)
  (declare (optimize speed))
  (atan! (zero) vec))
