(in-package #:%syntex.synthesizers.wfc)

(defclass topology-data ()
  ((%topology :reader topology
              :initarg :topology)
   (%data :reader data
          :initarg :data)))

(defun make-topology-data (topology data)
  (etypecase data
    ((simple-array t (*))
     (make-instance 'topology-data-1d :topology topology :data data))
    ((simple-array t (* *))
     (make-instance 'topology-data-2d :topology topology :data data))
    ((simple-array t (* * *))
     (make-instance 'topology-data-3d :topology topology :data data))))

(defgeneric make-topology-data-by-coords (topology func)
  (:method ((topology top:topology) (func function))
    (let* ((width (top:width topology))
           (height (top:height topology))
           (depth (top:depth topology))
           (data (make-array (list width height depth))))
      (dotimes (z depth)
        (dotimes (y height)
          (dotimes (x width)
            (let* ((point (point:point x y z))
                   (index (top:get-index topology point)))
              (when (top:contains-index-p topology index)
                (setf (aref data x y z) (funcall func point)))))))
      (make-topology-data topology data))))

(defgeneric make-topology-data-by-index (topology func)
  (:method ((topology top:topology) (func function))
    (let ((data (make-array (top:index-count topology))))
      (dolist (i (top:get-indices topology))
        (setf (aref data i) (funcall func i)))
      (make-topology-data topology data))))

(defgeneric to-2d-array (topology-data)
  (:method ((topology-data topology-data))
    (let* ((topology (topology topology-data))
           (width (top:width topology))
           (height (top:height topology))
           (array (make-array (list width height))))
      (dotimes (x width)
        (dotimes (y height)
          (let ((point (point:point x y)))
            (setf (aref array x y) (get-value topology-data point)))))
      array)))

(defgeneric to-3d-array (topology-data)
  (:method ((topology-data topology-data))
    (let* ((topology (topology topology-data))
           (width (top:width topology))
           (height (top:height topology))
           (depth (top:depth topology))
           (array (make-array (list width height depth))))
      (dotimes (x width)
        (dotimes (y height)
          (dotimes (z depth)
            (let ((point (point:point x y z)))
              (setf (aref array x y z) (get-value topology-data point))))))
      array)))

(defgeneric get-value (topology-data point/index))
