(in-package :gamebox-math)

(deftype mat4 () '(simple-array single-float (16)))

(defstruct (mat4 (:type (vector single-float))
                 (:constructor %mat4 (m00 m01 m02 m03
                                      m10 m11 m12 m13
                                      m20 m21 m22 m23
                                      m30 m31 m32 m33))
                 (:conc-name nil)
                 (:copier nil)
                 (:predicate nil))
  (m00 0.0f0 :type single-float)
  (m10 0.0f0 :type single-float)
  (m20 0.0f0 :type single-float)
  (m30 0.0f0 :type single-float)
  (m01 0.0f0 :type single-float)
  (m11 0.0f0 :type single-float)
  (m21 0.0f0 :type single-float)
  (m31 0.0f0 :type single-float)
  (m02 0.0f0 :type single-float)
  (m12 0.0f0 :type single-float)
  (m22 0.0f0 :type single-float)
  (m32 0.0f0 :type single-float)
  (m03 0.0f0 :type single-float)
  (m13 0.0f0 :type single-float)
  (m23 0.0f0 :type single-float)
  (m33 0.0f0 :type single-float))

(declaim (inline mat4))
(defun* (mat4 -> mat4) ((m00 real) (m01 real) (m02 real) (m03 real)
                        (m10 real) (m11 real) (m12 real) (m13 real)
                        (m20 real) (m21 real) (m22 real) (m23 real)
                        (m30 real) (m31 real) (m32 real) (m33 real))
  (%mat4 (float m00 1.0f0)
         (float m01 1.0f0)
         (float m02 1.0f0)
         (float m03 1.0f0)
         (float m10 1.0f0)
         (float m11 1.0f0)
         (float m12 1.0f0)
         (float m13 1.0f0)
         (float m20 1.0f0)
         (float m21 1.0f0)
         (float m22 1.0f0)
         (float m23 1.0f0)
         (float m30 1.0f0)
         (float m31 1.0f0)
         (float m32 1.0f0)
         (float m33 1.0f0)))

(defmacro with-mat4 (binds &body body)
  (if (null binds)
      `(progn ,@body)
      (let ((prefix (caar binds)))
        `(with-accessors ((,prefix identity)
                          (,(make-accessor-symbol prefix ".00") m00)
                          (,(make-accessor-symbol prefix ".01") m01)
                          (,(make-accessor-symbol prefix ".02") m02)
                          (,(make-accessor-symbol prefix ".03") m03)
                          (,(make-accessor-symbol prefix ".10") m10)
                          (,(make-accessor-symbol prefix ".11") m11)
                          (,(make-accessor-symbol prefix ".12") m12)
                          (,(make-accessor-symbol prefix ".13") m13)
                          (,(make-accessor-symbol prefix ".20") m20)
                          (,(make-accessor-symbol prefix ".21") m21)
                          (,(make-accessor-symbol prefix ".22") m22)
                          (,(make-accessor-symbol prefix ".23") m23)
                          (,(make-accessor-symbol prefix ".30") m30)
                          (,(make-accessor-symbol prefix ".31") m31)
                          (,(make-accessor-symbol prefix ".32") m32)
                          (,(make-accessor-symbol prefix ".33") m33))
             ,(cadar binds)
           (with-mat4 ,(cdr binds) ,@body)))))

(set-pprint-dispatch
 'mat4
 (lambda (stream object)
   (print-unreadable-object (object stream)
     (with-mat4 ((m object))
       (format stream "~f ~f ~f ~f~%  ~f ~f ~f ~f~%  ~f ~f ~f ~f~%  ~f ~f ~f ~f"
               m.00 m.01 m.02 m.03
               m.10 m.11 m.12 m.13
               m.20 m.21 m.22 m.23
               m.30 m.31 m.32 m.33))))
 1)
