(in-package #:%mfiano.graphics.image.color)

;;; Helpers.

(defmacro with-convert ((from to) &body body)
  `(with-pool-colors (,@(butlast (rest (car body))))
     ,@(loop :with last := (first (last (car body)))
             :for (x y) :on (car body) :by #'cdr
             :for first := from :then nil
             :for op := (u:symbolicate x '#:-> y)
             :unless (or (eq x last) (eq y last))
               :collect `(,op ,(if first from x) ,y)
             :when (and y (eq y last))
               :collect `(,op ,x ,to))))

(defun get-rgb-transform (from to illuminant-name)
  (let* ((transforms (base:rgb-transforms base:*context*))
         (from-space (space-name from))
         (to-space (space-name to))
         (key (list from-space to-space illuminant-name)))
    (declare (dynamic-extent key))
    (labels ((make-vector (x y)
               (let ((x (/ x y))
                     (z (/ (- 1 x y) y)))
                 (declare (u:f64 x z))
                 (v3:vec x 1 z)))
             (calculate-rgb-transform (rgb-space)
               (destructuring-bind ((rx ry) (gx gy) (bx by)) (coords rgb-space)
                 (declare (u:f64 rx ry gx gy bx by))
                 (let* ((r (make-vector rx ry))
                        (g (make-vector gx gy))
                        (b (make-vector bx by))
                        (scale (m3:*v3 (m3:invert (m3:mat r g b))
                                       (get-white-point illuminant-name))))
                   (m3:mat (v3:scale r (v3:x scale))
                           (v3:scale g (v3:y scale))
                           (v3:scale b (v3:z scale))))))
             (calculate-transform ()
               (etypecase from
                 (rgb (calculate-rgb-transform from))
                 (xyz (m3:invert (calculate-rgb-transform to))))))
      (declare (inline make-vector calculate-transform))
      (u:if-found (transform (u:href transforms key))
        transform
        (setf (u:href transforms (copy-list key)) (calculate-transform))))))

(declaim (inline get-rgb-hue-properties))
(defun get-rgb-hue-properties (rgb)
  (let ((rgb-channels (channels rgb)))
    (declare (v3:vec rgb-channels))
    (v3:with-components ((rgb- rgb-channels))
      (let* ((min (min rgb-x rgb-y rgb-z))
             (max (max rgb-x rgb-y rgb-z))
             (delta (- max min)))
        (values
         (cond
           ((zerop delta)
            0d0)
           ((= rgb-x max)
            (* (mod (/ (- rgb-y rgb-z) delta) 6d0) 60d0))
           ((= rgb-y max)
            (* (+ (/ (- rgb-z rgb-x) delta) 2d0) 60d0))
           (t
            (* (+ (/ (- rgb-x rgb-y) delta) 4d0) 60d0)))
         min
         max
         delta)))))

(declaim (inline get-rgb-from-hue))
(defun get-rgb-from-hue/chroma (hue chroma)
  (let ((x (* chroma (- 1d0 (abs (1- (mod (/ hue 60d0) 2d0)))))))
    (cond
      ((or (zerop hue)
           (< hue 60d0))
       (values chroma x 0d0))
      ((< hue 120d0)
       (values x chroma 0d0))
      ((< hue 180d0)
       (values 0d0 chroma x))
      ((< hue 240d0)
       (values 0d0 x chroma))
      ((< hue 300d0)
       (values x 0d0 chroma))
      ((< hue 360d0)
       (values chroma 0d0 x)))))

;; Allow converting to a model given a symbol, which denotes a model with default properties.

(defmethod base:convert ((from color) (to symbol))
  (base:convert from (make-color to)))

;;; Handle converting from/to color models with alpha channels.

(defmethod base:convert :around ((from alpha) (to color))
  (with-pool-color (alpha (type-of from) :space (space-name from) :copy from)
    (when (pre-multiplied-alpha-p from)
      (un-pre-multiply-alpha alpha))
    (call-next-method alpha to)))

(defmethod base:convert :after ((from color) (to alpha))
  (let ((to-channels (channels to))
        (to-index (alpha-index to)))
    (if (typep from 'alpha)
        (setf (aref to-channels to-index) (aref (channels from) (alpha-index from)))
        (setf (aref to-channels to-index) 1d0))
    (when (pre-multiply-alpha to)
      (pre-multiply-alpha to))))

;;; Low level conversion routines.

(declaim (inline hsl->rgb))
(defun hsl->rgb (hsl rgb)
  (let ((hsl-channels (channels hsl))
        (rgb-channels (channels rgb)))
    (declare (v3:vec hsl-channels rgb-channels))
    (v3:with-components ((hsl- hsl-channels)
                         (rgb- rgb-channels))
      (u:mvlet* ((c (* (- 1d0 (abs (1- (* hsl-z 2d0)))) hsl-y))
                 (m (- hsl-z (/ c 2)))
                 (r g b (get-rgb-from-hue/chroma hsl-x c)))
        (copy-illuminant-name hsl rgb)
        (setf rgb-x (+ r m)
              rgb-y (+ g m)
              rgb-z (+ b m))
        rgb))))

(declaim (inline hsv->rgb))
(defun hsv->rgb (hsv rgb)
  (let ((hsv-channels (channels hsv))
        (rgb-channels (channels rgb)))
    (declare (v3:vec hsv-channels rgb-channels))
    (v3:with-components ((hsv- hsv-channels)
                         (rgb- rgb-channels))
      (u:mvlet* ((c (* hsv-y hsv-z))
                 (m (- hsv-z c))
                 (r g b (get-rgb-from-hue/chroma hsv-x c)))
        (copy-illuminant-name hsv rgb)
        (setf rgb-x (+ r m)
              rgb-y (+ g m)
              rgb-z (+ b m))
        rgb))))

(declaim (inline lab->lchab))
(defun lab->lchab (lab lchab)
  (declare (optimize speed))
  (let ((lab-channels (channels lab))
        (lch-channels (channels lchab)))
    (declare (v3:vec lab-channels lch-channels))
    (v3:with-components ((lab- lab-channels)
                         (lch- lch-channels))
      (let ((arctan (atan lab-z lab-y)))
        (copy-illuminant-name lab lchab)
        (setf lch-x lab-x
              lch-y (sqrt (+ (expt lab-y 2) (expt lab-z 2)))
              lch-z (if (plusp arctan)
                        (u:radians->degrees arctan)
                        (+ (u:radians->degrees arctan) 360)))
        lchab))))

(declaim (inline lab->xyz))
(defun lab->xyz (lab xyz)
  (declare (optimize speed))
  (let ((lab-channels (channels lab))
        (xyz-channels (channels xyz))
        (white-point (get-white-point (illuminant-name lab))))
    (declare (v3:vec xyz-channels white-point))
    (v3:with-components ((xyz- xyz-channels)
                         (w white-point))
      (let* ((L (v3:x lab-channels))
             (a (v3:y lab-channels))
             (b (v3:z lab-channels))
             (fy (* (+ L 16) #.(/ 116)))
             (fx (+ (* a #.(/ 500)) fy))
             (fz (- fy (* b #.(/ 200))))
             (fx^3 (expt fx 3))
             (fz^3 (expt fz 3))
             (rx (if (> fx^3 +cie-e+)
                     fx^3
                     (* (- (* fx 116) 16) #.(/ +cie-k+))))
             (ry (if (> L #.(* +cie-k+ +cie-e+))
                     (expt (* (+ L 16) #.(/ 116)) 3)
                     (* L #.(/ +cie-k+))))
             (rz (if (> fz^3 +cie-e+)
                     fz^3
                     (* (- (* fz 116) 16) #.(/ +cie-k+)))))
        (copy-illuminant-name lab xyz)
        (setf xyz-x (* rx wx)
              xyz-y (* ry wy)
              xyz-z (* rz wz))
        xyz))))

(declaim (inline lchab->lab))
(defun lchab->lab (lchab lab)
  (declare (optimize speed))
  (let ((lch-channels (channels lchab))
        (lab-channels (channels lab)))
    (declare (v3:vec lch-channels lab-channels))
    (v3:with-components ((lch- lch-channels)
                         (lab- lab-channels))
      (let ((h (u:degrees->radians lch-z)))
        (copy-illuminant-name lchab lab)
        (setf lab-x lch-x
              lab-y (* lch-y (cos h))
              lab-z (* lch-y (sin h)))
        lab))))

(declaim (inline lchuv->luv))
(defun lchuv->luv (lchuv luv)
  (declare (optimize speed))
  (let ((lch-channels (channels lchuv))
        (luv-channels (channels luv)))
    (declare (v3:vec lch-channels luv-channels))
    (v3:with-components ((lch- lch-channels)
                         (luv- luv-channels))
      (let ((h (u:degrees->radians lch-z)))
        (copy-illuminant-name lchuv luv)
        (setf luv-x lch-x
              luv-y (* lch-y (cos h))
              luv-z (* lch-y (sin h)))
        luv))))

(declaim (inline luv->lchuv))
(defun luv->lchuv (luv lchuv)
  (declare (optimize speed))
  (let ((luv-channels (channels luv))
        (lch-channels (channels lchuv)))
    (declare (v3:vec luv-channels lch-channels))
    (v3:with-components ((luv- luv-channels)
                         (lch- lch-channels))
      (let ((arctan (atan luv-z luv-y)))
        (copy-illuminant-name luv lchuv)
        (setf lch-x luv-x
              lch-y (sqrt (+ (expt luv-y 2) (expt luv-z 2)))
              lch-z (if (plusp arctan)
                        (u:radians->degrees arctan)
                        (+ (u:radians->degrees arctan) 360)))
        lchuv))))

(declaim (inline luv->xyz))
(defun luv->xyz (luv xyz)
  (declare (optimize speed))
  (let ((luv-channels (channels luv))
        (xyz-channels (channels xyz))
        (white-point (get-white-point (illuminant-name luv))))
    (declare (v3:vec luv-channels xyz-channels white-point))
    (v3:with-components ((luv- luv-channels)
                         (xyz- xyz-channels)
                         (w white-point))
      (when (zerop luv-x)
        (v3:zero! xyz-channels)
        (return-from luv->xyz xyz))
      (let* ((div (+ wx (* wy 15) (* wz 3)))
             (a (* (1- (/ (* 52 luv-x) (+ luv-y (* 13 luv-x (/ (* wx 4) div))))) #.(/ 3)))
             (y (if (> luv-x #.(* +cie-k+ +cie-e+))
                    (expt (* (+ luv-x 16) #.(/ 116)) 3)
                    (* luv-x #.(/ +cie-k+))))
             (d (* (- (/ (* 39 luv-x) (+ luv-z (* 13 luv-x (/ (* wy 9) div)))) 5) y))
             (-5y (* y -5)))
        (copy-illuminant-name luv xyz)
        (setf xyz-x (/ (- d -5y) (- a -1/3))
              xyz-y y
              xyz-z (+ (* xyz-x a) -5y))
        xyz))))

(declaim (inline rgb->hsl))
(defun rgb->hsl (rgb hsl)
  (let ((hsl-channels (channels hsl)))
    (declare (v3:vec hsl-channels))
    (v3:with-components ((hsl- hsl-channels))
      (u:mvlet* ((h min max delta (get-rgb-hue-properties rgb))
                 (l (* (+ max min) 0.5d0))
                 (s (if (zerop l) 0d0 (/ delta (- 1d0 (abs (1- (* l 2d0))))))))
        (copy-illuminant-name rgb hsl)
        (setf hsl-x h
              hsl-y s
              hsl-z l)
        hsl))))

(declaim (inline rgb->hsv))
(defun rgb->hsv (rgb hsv)
  (let ((hsv-channels (channels hsv)))
    (declare (v3:vec hsv-channels))
    (v3:with-components ((hsv- hsv-channels))
      (u:mvlet ((h min max delta (get-rgb-hue-properties rgb)))
        (copy-illuminant-name rgb hsv)
        (setf hsv-x h
              hsv-y (if (zerop max) 0d0 (/ delta max))
              hsv-z max)
        hsv))))

(declaim (inline rgb->xyz))
(defun rgb->xyz (rgb xyz)
  (declare (optimize speed))
  (with-pool-color (temp-rgb (type-of rgb) :space (space-name rgb) :copy rgb)
    (let* ((illuminant-name (illuminant-name temp-rgb))
           (transform (get-rgb-transform temp-rgb xyz illuminant-name))
           (rgb-channels (v3:zero)))
      (declare (dynamic-extent rgb-channels))
      (linearize-rgb temp-rgb)
      (replace rgb-channels (the (or v3:vec v4:vec) (channels temp-rgb)))
      (m3:*v3! (channels xyz) transform rgb-channels)
      (setf (%illuminant-name xyz) illuminant-name)
      xyz)))

(declaim (inline xyy->xyz))
(defun xyy->xyz (xyy xyz)
  (declare (optimize speed))
  (let ((xyy-channels (channels xyy))
        (xyz-channels (channels xyz)))
    (declare (v3:vec xyy-channels xyz-channels))
    (v3:with-components ((xyy- xyy-channels)
                         (xyz- xyz-channels))
      (copy-illuminant-name xyy xyz)
      (if (zerop xyy-y)
          (v3:zero! xyz-channels)
          (setf xyz-x (/ (* xyy-x xyy-z) xyy-y)
                xyz-y xyy-z
                xyz-z (/ (* (- 1 xyy-x xyy-y) xyy-z) xyy-y)))
      xyz)))

(declaim (inline xyz->lab))
(defun xyz->lab (xyz lab)
  (declare (optimize speed))
  (let* ((lab-channels (channels lab))
         (r (v3:/ (channels xyz) (get-white-point (illuminant-name xyz))))
         (rx (v3:x r))
         (ry (v3:y r))
         (rz (v3:z r))
         (fx (if (> rx +cie-e+)
                 (expt rx #.(/ 3d0))
                 (* (+ (* +cie-k+ rx) 16) #.(/ 116))))
         (fy (if (> ry +cie-e+)
                 (expt ry #.(/ 3d0))
                 (* (+ (* +cie-k+ ry) 16) #.(/ 116))))
         (fz (if (> rz +cie-e+)
                 (expt rz #.(/ 3d0))
                 (* (+ (* +cie-k+ rz) 16) #.(/ 116)))))
    (declare (v3:vec lab-channels)
             (dynamic-extent r))
    (v3:with-components ((lab- lab-channels))
      (copy-illuminant-name xyz lab)
      (setf lab-x (- (* fy 116) 16)
            lab-y (* (- fx fy) 500)
            lab-z (* (- fy fz) 200))
      lab)))

(declaim (inline xyz->luv))
(defun xyz->luv (xyz luv)
  (declare (optimize speed))
  (let ((xyz-channels (channels xyz))
        (luv-channels (channels luv))
        (white-point (get-white-point (illuminant-name xyz))))
    (declare (v3:vec xyz-channels luv-channels white-point))
    (v3:with-components ((xyz- xyz-channels)
                         (luv- luv-channels)
                         (w white-point))
      (let ((reference-y (/ xyz-y wy))
            (div-uv (+ xyz-x (* xyz-y 15) (* xyz-z 3)))
            (div-w (+ wx (* wy 15) (* wz 3))))
        (copy-illuminant-name xyz luv)
        (if (zerop div-uv)
            (v3:zero! luv-channels)
            (setf luv-x (if (> reference-y +cie-e+)
                            (- (* 116 (expt reference-y #.(/ 3d0))) 16)
                            (* +cie-k+ reference-y))
                  luv-y (* luv-x 13 (- (/ (* xyz-x 4) div-uv) (/ (* wx 4) div-w)))
                  luv-z (* luv-x 13 (- (/ (* xyz-y 9) div-uv) (/ (* wy 9) div-w)))))
        luv))))

(declaim (inline xyz->rgb))
(defun xyz->rgb (xyz rgb)
  (declare (optimize speed))
  (with-pool-color (temp-xyz 'xyz :copy xyz)
    (let* ((illuminant-name (illuminant-name rgb))
           (transform (get-rgb-transform temp-xyz rgb illuminant-name))
           (rgb-channels (v3:zero)))
      (declare (dynamic-extent rgb-channels))
      (adapt-chromaticity temp-xyz illuminant-name)
      (m3:*v3! rgb-channels transform (channels temp-xyz))
      (replace (the (or v3:vec v4:vec) (channels rgb)) rgb-channels)
      (delinearize-rgb rgb)
      (setf (%illuminant-name rgb) illuminant-name)
      rgb)))

(declaim (inline xyz->xyy))
(defun xyz->xyy (xyz xyy)
  (declare (optimize speed))
  (let ((xyz-channels (channels xyz))
        (xyy-channels (channels xyy)))
    (declare (v3:vec xyz-channels xyy-channels))
    (v3:with-components ((xyz- xyz-channels)
                         (xyy- xyy-channels))
      (let ((x+y+z (+ xyz-x xyz-y xyz-z)))
        (copy-illuminant-name xyz xyy)
        (setf xyy-x (if (zerop x+y+z) 0d0 (/ xyz-x x+y+z))
              xyy-y (if (zerop x+y+z) 0d0 (/ xyz-y x+y+z))
              xyy-z xyz-y)
        xyy))))
