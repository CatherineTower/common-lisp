(in-package #:%mfiano.graphics.tools.image.color)

(defun ensure-color-pool (model space)
  (declare (optimize speed))
  (let ((pools base:*worker-pools*)
        (key (cons model space)))
    (declare (dynamic-extent key))
    (or (u:href pools key)
        (let ((array (make-array 2 :adjustable t :fill-pointer 0)))
          (dotimes (i (length array))
            (setf (aref array i) (make-model model space)))
          (setf (u:href pools (copy-list key)) array)
          array))))

(declaim (inline reset-pool-color))
(defun reset-pool-color (color)
  (zero-channels color)
  (copy-illuminant-name color color t)
  (values))

(defun get-pool-color (model &key space copy)
  (declare (optimize speed))
  (let ((pool (ensure-color-pool model space)))
    (declare ((vector t) pool))
    (when (zerop (fill-pointer pool))
      (map-into pool (lambda () (make-model model space))))
    (let ((color (vector-pop pool)))
      (if copy
          (copy-channels copy color)
          (reset-pool-color color))
      color)))

(defun put-pool-color (color)
  (declare (optimize speed))
  (let ((pool (ensure-color-pool (type-of color) (space-name color))))
    (declare (vector pool))
    (vector-push-extend color pool (array-total-size pool))
    (values)))

(defmacro with-pool-color ((binding model &key space copy) &body body)
  `(if (boundp 'base:*worker-pools*)
       (let ((,binding (get-pool-color ,model :space ,(or space model) :copy ,copy)))
         (unwind-protect (progn ,@body)
           (put-pool-color ,binding)))
       (let ((,binding (make-model ,model ,(or space model))))
         ,@(when copy
             `((copy-channels ,copy ,binding)))
         ,@body)))
