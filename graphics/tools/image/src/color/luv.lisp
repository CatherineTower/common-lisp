(in-package #:mfiano.graphics.tools.image.color)

(u:define-constant +epsilon/luv+ #.(float 216/24389 1d0))
(u:define-constant +kappa/luv+ #.(float 24389/27 1d0))

(defun %u-chromaticity (color-components)
  (m:with-vector ((3 color- color-components)) ()
    (/ (* color-x 4)
       (+ color-x (* color-y 15) (* color-z 3)))))

(defun %v-chromaticity (color-components)
  (m:with-vector ((3 color- color-components)) ()
    (/ (* color-y 9)
       (+ color-x (* color-y 15) (* color-z 3)))))

(defun %luv->xyz (in out)
  (flet ((%a (luv-channels illuminant)
           (m:with-vector ((3 luv- luv-channels)) ()
             (/ (1- (/ (* 52 luv-x)
                       (+ luv-y (* 13 luv-x (%u-chromaticity illuminant)))))
                3)))

         (%d (luv-channels y illuminant)
           (m:with-vector ((3 luv- luv-channels)) ()
             (* (- (/ (* 39 luv-x)
                      (+ luv-z (* 13 luv-x (%v-chromaticity illuminant))))
                   5)
                y)))
         (%y (luv-channels)
           (m:with-vector ((3 luv- luv-channels)) ()
             (if (> luv-x (* +kappa/luv+ +epsilon/luv+))
                 (expt (/ (+ luv-x 16) 116) 3)
                 (/ luv-x +kappa/luv+)))))
    (declare (inline %a %d %y))
    (let ((source-channels (data in))
          (illuminant (get-white-point (illuminant-name in))))
      (when (zerop (m:x source-channels))
        (error "Illegal L value in Luv ~A" in))
      (let* ((a (%a source-channels illuminant))
             (y (%y source-channels))
             (-5y (* y -5))
             (x (/ (- (%d source-channels y illuminant) -5y)
                   (- a -1/3)))
             (z (+ (* x a) -5y)))
        (m:with-vector ((3 xyz- (data out))) (:read-only nil)
          (setf xyz-x x
                xyz-y y
                xyz-z z)))
      out)))

(defun %xyz->luv (in out)
  (let* ((source-channels (data in))
         (illuminant (get-white-point (illuminant-name in)))
         (reference-y (/ (m:y source-channels) (m:y illuminant)))
         (l (if (> reference-y +epsilon/luv+)
                (- (* 116 (expt reference-y 1/3)) 16)
                (* +kappa/luv+ reference-y)))
         (u (* 13 l (- (%u-chromaticity source-channels)
                       (%u-chromaticity illuminant))))
         (v (* 13 l (- (%v-chromaticity source-channels)
                       (%v-chromaticity illuminant)))))
    (m:with-vector ((3 luv- (data out))) (:read-only nil)
      (setf luv-x l
            luv-y u
            luv-z v))
    out))
