(in-package #:mfiano.graphics.tools.color)

(defgeneric decompose (color))

(defmethod decompose ((color rgba))
  (declare (optimize speed))
  (flet ((%decompose (value alpha)
           (truncate (* (%or-shift8 value) alpha) #xff)))
    (declare (inline %decompose))
    (let ((a (rgba-a color)))
      (values (%decompose (rgba-r color) a)
              (%decompose (rgba-g color) a)
              (%decompose (rgba-b color) a)
              (%or-shift8 a)))))

(defmethod decompose ((color rgba-pma))
  (declare (optimize speed))
  (values (%or-shift8 (rgba-r color))
          (%or-shift8 (rgba-g color))
          (%or-shift8 (rgba-b color))
          (%or-shift8 (rgba-a color))))

(defmethod decompose ((color rgba16))
  (declare (optimize speed))
  (flet ((%decompose (value alpha)
           (truncate (* value alpha) #xffff)))
    (declare (inline %decompose))
    (let ((a (rgba16-a color)))
      (values (%decompose (rgba16-r color) a)
              (%decompose (rgba16-g color) a)
              (%decompose (rgba16-b color) a)
              a))))

(defmethod decompose ((color rgba16-pma))
  (declare (optimize speed))
  (values (rgba16-r color)
          (rgba16-g color)
          (rgba16-b color)
          (rgba16-a color)))

(defmethod decompose ((color alpha))
  (declare (optimize speed))
  (let ((a (%or-shift8 (alpha-value color))))
    (values a a a a)))

(defmethod decompose ((color alpha16))
  (declare (optimize speed))
  (let ((a (alpha16-value color)))
    (values a a a a)))

(defmethod decompose ((color gray))
  (declare (optimize speed))
  (let ((v (%or-shift8 (gray-value color))))
    (values v v v #xffff)))

(defmethod decompose ((color gray16))
  (declare (optimize speed))
  (let ((v (gray16-value color)))
    (values v v v #xffff)))
